// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Put your code here:

    // register always outputs its value and passes to out and the incrementor
    Register(in=resetOut, load=true, out=out, out=toInc);
    // incrementor always outputs register + 1
    Inc16(in=toInc, out=fromInc);

    // if inc then incremented value else register value
    Mux16(a=toInc, b=fromInc, sel=inc, out=incInc);
    // if load then input else incremented/not-incremented value
    Mux16(a=incInc, b=in, sel=load, out=loadOut);
    // if reset then output false else incremented/not-incremented value
    Mux16(a=loadOut, b=false, sel=reset, out=resetOut);
}
